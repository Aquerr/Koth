import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.PluginDependency

plugins {
    id 'java'
    id 'idea'
    id 'java-library'
    id 'org.spongepowered.gradle.plugin' version '1.1.1'
}

group 'io.github.aquerr'
version '0.5.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

repositories {
    mavenCentral()
}

dependencies {

}

jar {
    if(System.getenv("JENKINS_HOME")) {
        version = version + "_" + System.getenv("BUILD_NUMBER")
        println("File name => " + baseName)
    } else {
        version = version + "-SNAPSHOT"
    }
}

sponge {
    apiVersion ("8.0.0")
    plugin("koth") {
        loader(PluginLoaders.JAVA_PLAIN)
        displayName("Koth")
        mainClass("io.github.aquerr.koth.Koth")
        description("Create King of the Hill arenas!")
        links {
            homepage("https://github.com/Aquerr/Koth")
            source("https://github.com/Aquerr/Koth")
            issues("https://github.com/Aquerr/Koth/issues")
        }
        contributor("Aquerr") {
            description("Lead Developer")
        }
        contributor("PKNCK") {
            description("Developer")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}


tasks.create("publishBuildOnDiscord") {
    group = "Publishing"
    description = "Task for publishing the jar file to discord's jenkins channel"
    doLast {
        String[] jarFiles = new FileNameFinder().getFileNames(project.buildDir.getPath(), "**/*.jar")

        if(jarFiles.length > 0) {
            println("Found jar files: " + jarFiles)

            String lastCommitDescription = "git log -1 --pretty=%B".execute().getText()
            if(lastCommitDescription == null || lastCommitDescription == "") {
                lastCommitDescription = "No changelog provided"
            }

            exec {
                commandLine("java", "-jar", "..\\jenkinsdiscordbot-1.0.jar", "Koth", jarFiles[0], lastCommitDescription)
            }
        }
    }
}
